apply plugin: 'eclipse'

configurations {
	ajc
	aspects
	ajInpath
}

dependencies {
	ajc("org.aspectj:aspectjtools:1.6.10")
	ajc("org.aspectj:aspectjweaver:1.6.10")
	ajc("org.aspectj:aspectjrt:1.6.10")
	compile("org.aspectj:aspectjrt:1.6.10")
	runtime("org.aspectj:aspectjrt:1.6.10")
}

task compileJava(dependsOn: JavaPlugin.PROCESS_RESOURCES_TASK_NAME, overwrite: true) << {
	ant.taskdef( resource:"org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", classpath: configurations.ajc.asPath)
	ant.iajc(source:sourceCompatibility,
			target:targetCompatibility,
			destDir:sourceSets.main.classesDir.absolutePath,
			classpath: configurations.compile.asPath,
			aspectPath: configurations.aspects.asPath,
			inpath: configurations.ajInpath.asPath,
			debug: true,
			Xlint: "ignore",
			sourceRootCopyFilter:"**/.svn/*,**/*.java,**/*.aj") {
				sourceroots {
					sourceSets.main.java.srcDirs.each{
						pathelement(location:it.absolutePath)
					}
				}
			}
}

task compileTestJava(dependsOn: JavaPlugin.COMPILE_JAVA_TASK_NAME, overwrite: true) << {
	def iajcClasspath = ant.path {
		pathelement( location: configurations.testCompile.asPath)
		pathelement( location: sourceSets.main.classesDir.absolutePath)
	}
	def iajcAspectPath = ant.path {
		pathelement( location: configurations.aspects.asPath)
		pathelement( location: sourceSets.main.classesDir.absolutePath)
	}
	ant.taskdef( resource:"org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", classpath: configurations.ajc.asPath)
	ant.iajc(source:sourceCompatibility,
			target:targetCompatibility,
			destDir:sourceSets.test.classesDir.absolutePath,
			classpath: iajcClasspath,
			aspectPath: iajcAspectPath,
			inpath: configurations.ajInpath.asPath,
			debug: true,
			Xlint: "ignore",
			sourceRootCopyFilter:"**/.svn/*,**/*.java,**/*.aj") {
				sourceroots {
					sourceSets.test.java.srcDirs.each{
						pathelement(location:it.absolutePath)
					}
				}
			}
}



eclipseProject {
	buildCommand ('org.eclipse.ajdt.core.ajbuilder')
	natures('org.eclipse.ajdt.core.ajnature')
	natures('org.eclipse.ajdt.ui.ajnature')
}





